# dotfiles debug
export DOTFILES_DEBUG=true

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  if [ "$DOTFILES_DEBUG" = true ]; then echo "→ load path file: $file"; fi # debug
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  if [ "$DOTFILES_DEBUG" = true ]; then echo "→ load other file: $file"; fi # debug
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  if [ "$DOTFILES_DEBUG" = true ]; then echo "→ load completion file: $file"; fi # debug
  source $file
done

unset config_files

# iTerm2 Shell
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# enable pyenv
eval "$(pyenv init -)"
eval "$(pyenv virtualenv-init -)"